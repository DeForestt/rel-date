.needs <std>

import CArgs from "./src/CLineArgs.af";
import DateTime from "DateTime.af";
import String from "String.af";

import ICollection from "Collections";
import LinkedList from "Collections";
import {printChar, print, printInt} from "io" under io;

import {Now, mmddyyyyToDate} from "DateTime" under std_dt;
import { daysInMonth, daysSinceJan1, dateRelativeTo,  dayOfWeek, dowToString } from "./src/DateFunctions.af" under dt;
import {matches} from "./src/StringMatch" under match;


int printJulian(int year, int month, int day){
	int julian = dt.daysSinceJan1(month, day, year) + 1;
	io.printInt(year);
	// get the number of digits in julian
	int digits = 0;
	int temp = julian;
	while(temp > 0){
		temp = temp / 10;
		digits++;
	};

	for int i = digits; i < 3; i++
		io.printChar('0');
	io.printInt(julian);
};

int printDaysSince(int year, int month, int day, int relYear){
	int date = dt.dateRelativeTo(month, day, year, relYear);
	io.printInt(date);
};

bool validateDate(int year, int month, int day){
			if month < 1 | month > 12 {
			io.print("Invalid month\n");
			return false;
		};

		if day < 1 | day > 31 {
			io.print("Invalid day\n");
			return false;
		};

		if month == 2 & day > 29 {
			io.print("Invalid day\n");
			return false;
		};

		if month == 4 | month == 6 | month == 9 | month == 11 & day > 30 {
			io.print("Invalid day\n");
			return false;
		};
	return true;
};

int main(int argc, adr argv){
	CArgs args = new CArgs(argc, argv);

	if args.has("-h") |
	args.has("--help"){
		io.print("Usage: fdate <year> <month> <day> <options>\n");
		io.print("Options:\n");
		io.print("  -h, --help: io.print this help message\n");
		io.print("  -v, --version: io.print the version number\n");
		io.print("  -j, --julian: Use Julian calendar\n");
		io.print("  -r, --relative: io.print relative date\n");
		io.print("  -d, --day: io.print the day of the week\n");
		io.print("  \t eg fdate 2022 12 5 -r 1997\n");
		return 0;
	};

	if args.has("-v") |
	args.has("--version"){
		io.print("fdate version 1.0.0\n");
		return 0;
	};

	int year;
	int month;
	int day;
	
	LinkedList dateStrings = args.getTaglessArgs();
	if dateStrings.size() == 0{
		DateTime today = std_dt.Now(-4);
		year = today.getYear();
		month = today.getMonth();
		day = today.getDay();
	} else if dateStrings.size() == 1{
		char formatChar;
		String dateString = dateStrings.get(0);
		if match.matches(dateString, "dd-dd-dddd"){
			formatChar = '-';
		} else if match.matches(dateString, "dd/dd/dddd"){
			formatChar = '/';
		} else if match.matches(dateString, "dd.dd.dddd"){
			formatChar = '.';
		} else {
			io.print("Invalid date format\n");
			return 1;
		};

		DateTime date = std_dt.mmddyyyyToDate(dateStrings.get(0), formatChar);
		year = date.getYear();
		month = date.getMonth();
		day = date.getDay();
	} else if dateStrings.size() == 3{

		String yearString = dateStrings.get(0);
		String monthString = dateStrings.get(1);
		String dayString = dateStrings.get(2);

		if !yearString.isNumeric() {
			io.print("Invalid year\n");
			return 1;
		};

		if !monthString.isNumeric() {
			io.print("Invalid month\n");
			return 1;
		};

		if !dayString.isNumeric() {
			io.print("Invalid day\n");
			return 1;
		};

		year = yearString.toInt();
		month = monthString.toInt();
		day = dayString.toInt();

		if !validateDate(year, month, day){
			return 1;
		};
	} else {
		io.print("Invalid number of arguments\n");
		return 1;
	};

	// io.print mm/dd/yyyy
	io.print("Formatted date for: ");
	io.printInt(month); io.print("/"); io.printInt(day); io.print("/"); io.printInt(year);
	io.print("\n\n");

	bool pass = false;
	// printJulian
	if args.has("-j") |
	args.has("--julian"){
		io.print("Julian date: ");
		printJulian(year, month, day); io.print("\n");
		pass = true;
	};

	if args.has("-r") |
	args.has("--relative"){
		// get the year to compare to
		int relYear = 0;

		if args.has("-r"){
			String relYearString = args.get("-r");
			if !relYearString.isNumeric() {
				io.print("Invalid year\n");
				return 1;
			};
			relYear = relYearString.toInt();
		} else if args.has("--relative"){
			String relYearString = args.get("--relative");
			if !relYearString.isNumeric() {
				io.print("Invalid year\n");
				return 1;
			};
			relYear = relYearString.toInt();
		};

		io.print("Days since Jan 1, ");
		io.printInt(relYear); io.print(": ");
		printDaysSince(year, month, day, relYear); io.print("\n");
		pass = true;
	};

	if args.has("-d") |
	args.has("--day"){
		io.print("Day of the week: ");
		int dow = dt.dayOfWeek(month, day, year);
		io.print(dt.dowToString(dow)); io.print("\n");
		pass = true;
	};

	if !pass {
		// io.print the juliandate
		io.print("Julian date: ");
		printJulian(year, month, day); io.print("\n");

		// io.print days since epoch
		io.print("Days since Epoch: ");
		printDaysSince(year, month, day, 1970); io.print("\n");

		// days sinc 1900
		io.print("Days since Jan 1, 1900: ");
		printDaysSince(year, month, day, 1900); io.print("\n");

		// dayse since 1957
		io.print("Days since Jan 1, 1957: ");
		printDaysSince(year, month, day, 1957); io.print("\n");
	};

	return 0;
};
